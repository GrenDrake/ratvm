// ////////////////////////////////////////////////////////////////////////////
// Core and general game systems
// ////////////////////////////////////////////////////////////////////////////

// ////////////////////////////////////////////////////////////////////////////
// Constant definitions
// ////////////////////////////////////////////////////////////////////////////

value proSubject    0;
value proObject     1;
value proPos        2;
value proAdject     3;
value proReflex     4;

value skillMelee    0;
value skillRanged   1;
value skillMagic    2;

value dtBlunt       "blunt";
value dtCutting     "cutting";
value dtPiercing    "piercing";
value dtFire        "fire";
value dtAir         "air";
value dtEarth       "earth";
value dtWater       "water";

// ////////////////////////////////////////////////////////////////////////////
// Common strings
// ////////////////////////////////////////////////////////////////////////////

value hoursPerDay       1440;
value minutesPerHour    60;
value continueStr       "Continue";

object sexNeuter
    article             "a "
    name                "neuter"
    pronouns            [ "it" "it" "its" "its" "itself" ]
;
object sexMale
    article             "a "
    name                "male"
    pronouns            [ "he" "him" "his" "his" "himself" ]
;
object sexFemale
    article             "a "
    name                "female"
    pronouns            [ "she" "her" "her" "hers" "herself" ]
;

object speciesHuman
    article             "a "
    name                "human"
;
object speciesGnoll
    article             "a "
    name                "gnoll"
;
object speciesDragon
    article             "a "
    name                "dragon"
;

object thePlayer
    article             "the "
    name                "player"
    sex                 sexNeuter
    species             speciesHuman
    faction             0
    skills {
        skillMelee:     10
        skillRanged:    12
        skillMagic:     8
    }
;

object world
    time                720 // start game at noon
    location            0
    locationName        0
    inLocation          0
    newLocation         0
;


// ////////////////////////////////////////////////////////////////////////////
// Utility functions
// ////////////////////////////////////////////////////////////////////////////

function addContinue() {
    asm
    continueStr $location world get_prop add_option
}

function dispatch(event extra : result) {
    asm

    runevent:
    extra 1 $body event call_method
    result store
    result typeof Object cmp doneevent jnz
    result event store
    0 extra store
    runevent jmp

    doneevent:

    0 $inLocation world set_prop
    0 $newLocation world set_prop

    $location event get_prop result store
    result 0 cmp endlocation jz
    1 $inLocation world set_prop
    $location world get_prop event cmp isnewlocation jnz
    endlocation jmp

    isnewlocation:
    1 $newLocation world set_prop
    event $location world set_prop
    result $locationName world set_prop
    result InfobarLeft set_info



    endlocation:
//    "\nIN LOCATION: " say $inLocation world get_prop say
//    "\nNEW LOCATION: " say $newLocation world get_prop say
//    "\nLOCATION NAME: " say $locationName world get_prop say
    dispatch get_option
}

function main() {
    asm

    "Caged Gnoll" InfobarTitle set_info
    start 1 dispatch call
}

function printObj(theObject) {
    asm
    $article theObject get_prop say
    $name theObject get_prop say
}

function skillCheck(who skill modifier) {
    asm
    modifier typeof None cmp hasmod jnz
    0 modifier store
    hasmod:
    skill $skills who get_prop get_item
    "[(" say stack_dup say "+" say modifier say ")=" say
    modifier add stack_dup say
    " vs (" say

    1 7 random stack_dup say "+" say
    1 7 random stack_dup say "+" say add
    1 7 random stack_dup say ")=" say add
    stack_dup say

    cmp success jgte
    " = fail]" say
    0 return

    success:
    1 " = success]" say
}


// ////////////////////////////////////////////////////////////////////////////
// Startup and chargen
// ////////////////////////////////////////////////////////////////////////////

object start
    body function() {
        asm
        "Welcome to game!\nYou can select options by clicking on them or (for options up to ten) by pressing the appropriate number key." say
        "Start game" chargen1 add_option
    }
;

object chargen1
    body function() {
        asm
        "What sex are you?" say
        "Male"   chargen2   sexMale    add_option_x
        "Female" chargen2   sexFemale  add_option_x
    }
;
object chargen2
    body function(choice) {
        asm
        choice $sex thePlayer set_prop

        "What species are you?" say
        "Human"     chargen3    speciesHuman   add_option_x
        "Gnoll"     chargen3    speciesGnoll   add_option_x
        "Dragon"    chargen3    speciesDragon  add_option_x
    }
;
object chargen3
    body function(choice) {
        asm
        choice $species thePlayer set_prop

        "You are " say
        $sex thePlayer get_prop 1 printObj call
        " " say
        $name $species thePlayer get_prop get_prop say
        ". Beginning the game now.\n[b]----------------------------------------[/b]\n" say
        forestPath
    }
;


// ////////////////////////////////////////////////////////////////////////////
// General item definitions
// ////////////////////////////////////////////////////////////////////////////

object gold
    article     "a "
    name        "gold piece"
    plural      "gold pieces"
    description "The coin of the realm."
;

object loincloth
    article     "a "
    name        "loincloth"
    plural      "loincloths"
    slot        "body"
;


// ////////////////////////////////////////////////////////////////////////////
// Generic nodes for resting
// ////////////////////////////////////////////////////////////////////////////

object restAwhile
    body function() {
        asm
        "How long would you like to rest for?" say
        "One hour"      restAwhileDoit    60  add_option_x
        "Six hours"     restAwhileDoit    360 add_option_x
        "Twelve hours"  restAwhileDoit    720 add_option_x
        "Don't rest" $location world get_prop add_option
    }
;

object restAwhileDoit
    body function(length) {
        asm
        "You rest for a while. " say
//        length do_rest
        0 addContinue call
    }
;


include "g_forest.inc";
include "g_town.inc";
